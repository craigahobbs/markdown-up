# Licensed under the MIT License
# https://github.com/craigahobbs/bare-script/blob/main/LICENSE

include <args.bare>
include <schemaDoc.bare>


# $function: schemaDocAppMain
# $group: schemaDocApp.bare
# $doc: The Schema Markdown documentation viewer main entry point
# $arg url: Optional (default is null). The Schema Markdown text or JSON resource URL. If null, the Schema Markdown type model is displayed.
# $arg title: Optional. The schema title.
# $arg hideNoGroup: Optional (default is false). If true, hide types with no group.
async function schemaDocAppMain(url, title, hideNoGroup):
    # Parse arguments
    args = argsParse(schemaDocAppArguments)
    name = objectGet(args, 'name')
    url = objectGet(args, 'url', url)
    title = if(title != null && !objectHas(args, 'url'), title, url)

    # If no URL was provided, use the Schema Markdown type model schema
    if url == null || url == '':
        types = schemaTypeModel()
        title = 'The Schema Markdown Type Model'
    else:
        # Fetch the Schema Markdown resource
        types = null
        schemaText = systemFetch(url)
        if schemaText != null:
            if stringEndsWith(url, '.json'):
                schemaJSON = jsonParse(schemaText)
                if schemaJSON != null:
                    types = schemaValidateTypeModel(schemaJSON)
                endif
            else:
                types = schemaParse(schemaText)
            endif
        endif

        # Error?
        if types == null:
            markdownPrint('**Error:** Failed to fetch Schema Markdown resource "' + url + '"')
            return
        endif
    endif

    # Render the page
    if name != null:
        schemaDocAppTypePage(types, title, name)
    else:
        schemaDocAppIndexPage(args, types, title, hideNoGroup)
    endif
endfunction


# The Schema Markdown documentation viewer arguments
schemaDocAppArguments = argsValidate([ \
    {'name': 'name'}, \
    {'name': 'publish', 'type': 'bool', 'default': false}, \
    {'name': 'single', 'type': 'bool', 'default': false}, \
    {'name': 'url', 'global': 'vURL'} \
])


# Render the Schema Markdown documentation viewer index page
function schemaDocAppIndexPage(args, types, title, hideNoGroup):
    publish = objectGet(args, 'publish')
    single = objectGet(args, 'single')

    # Set the page title
    documentSetTitle(title)
    markdownPrint('# ' + markdownEscape(title))

    # Render the single page toggle
    if !publish:
        markdownPrint('', argsLink(schemaDocAppArguments, if(single, 'Multi Page', 'Single Page'), {'single': !single}))
    endif

    # Group the types
    groups = {}
    typeNames = arraySort(objectKeys(types))
    typeGroups = {'action': 'Actions', 'enum': 'Enums', 'struct': 'Structs', 'typedef': 'Typedefs'}
    for typeName in typeNames:
        type = objectGet(types, typeName)
        group = objectGet(objectGet(type, arrayGet(objectKeys(type), 0)), 'docGroup')

        # No group? Use the type's default group.
        if group == null:
            if hideNoGroup:
                continue
            endif
            group = objectGet(typeGroups, arrayGet(objectKeys(type), 0))
        endif

        # Add the type to the group
        if !objectHas(groups, group):
            objectSet(groups, group, [])
        endif
        arrayPush(objectGet(groups, group), type)
    endfor
    groupNames = arraySort(objectKeys(groups))

    # The table of contents
    if single:
        markdownPrint('', '## Table of Contents', '')
        for groupName in groupNames:
            markdownPrint('- ' + argsLink(schemaDocAppArguments, groupName, null, false, groupName))
        endfor
    endif

    # Render the index groups
    for groupName in groupNames:
        if single:
            markdownPrint('', '---')
        endif
        markdownPrint('', '## ' + markdownEscape(groupName))
        if single && !publish:
            markdownPrint('', argsLink(schemaDocAppArguments, 'Back to top', null, false, '_top'))
        endif

        # Render the group type links
        groupTypes = objectGet(groups, groupName)
        for groupType in groupTypes:
            groupTypeName = objectGet(objectGet(groupType, arrayGet(objectKeys(groupType), 0)), 'name')
            if single:
                markdownPrint('', schemaDocMarkdown(types, groupTypeName, {'headerPrefix': '###', 'hideReferenced': true}))
            else:
                markdownPrint('', argsLink(schemaDocAppArguments, groupTypeName, {'name': groupTypeName}, false, '_top'))
            endif
        endfor
    endfor
endfunction


# Render the Schema Markdown documentation viewer type page
function schemaDocAppTypePage(types, title, typeName):
    # Set the page title
    documentSetTitle(title + ' - ' + typeName)
    markdownPrint(argsLink(schemaDocAppArguments, 'Index', {'name': null}))

    # Type exist?
    if !objectHas(types, typeName):
        markdownPrint('', '**Error:** Unknown type "' + typeName + '"')
        return
    endif

    # Render the type's documentation
    markdownPrint(schemaDocMarkdown(types, typeName))
endfunction
